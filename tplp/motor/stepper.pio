; PWM controller for a bipolar stepper motor.
;
; FJOIN_TX = 1
; OUT_SHIFTDIR = 1
; AUTOPULL = 1
; PULL_THRESH = 0 (meaning a value of 32)
;
; Command format:
;   pwm_period, t1, t2_t3, pins_t2_t3, pins_t4
.program stepper
.define public loop_delay 16
.define public instructions_per_count (loop_delay+2)

; # phase A1  A2  phase B1  B2
; 0   0   255 255   +   255 0
; 1   ^   255 dn    v   255 up
; 2   +   255 0     0   255 255
; 3   v   255 up    v   dn  255
; 4   0   255 255   -   0   255
; 5   v   dn  255   ^   up  255
; 6   -   0   255   0   255 255
; 7   ^   up  255   ^   255 dn
;     wrap around   wrap around


; XXX: ok... it's 1:30am... but i think this will work...
; should have no glitches. not sure if branch timings need adjusting.
; indicate zero t1 time by setting t1:=pwm_period.
; indicate zero t2_t3 time by setting t2_t3:=t1
; t4 time is the remainder. skip t4 by setting t2_t3:=0.
; NOTA BENE: i use t1/t2/t3/t4 ambiguously, to mean either:
;       1. a duration
;       2. a timestamp (value between 0 and pwm_period inclusive).
; TODO: characterize what the non-loop instructions mean w.r.t.
;       the pwm cycle. we may need to adjust t1/t2/t3/t4 when
;       generating commands.
new_command:
    out isr, 32
.wrap_target
    mov osr, isr
    out y, 8            ; y := pwm_period
    out x, 8            ; x := t1
    jmp x!=y t1
    jmp t1_break
t1:
    set pins, 0b1111    ; pins := pins_t1 (fixed)
t1_loop:
    jmp x!=y t1_continue
    jmp t1_break
t1_continue:
    jmp y--, t1_loop     [loop_delay]
t1_break:
    out x, 8            ; x := t2_t3
    jmp x!=y t2_t3
    out null, 4         ; discard pins_t2_t3 if t2_t3 time is zero
    jmp t2_t3_break
t2_t3:
    out pins, 4         ; pins := pins_t2_t3
t2_t3_loop:
    jmp x!=y t2_t3_continue
    jmp t2_t3_break
t2_t3_continue:
    jmp y--, t2_t3_loop  [loop_delay]
t2_t3_break:
    set x, 0            ; x := t4
    jmp x!=y t4
    jmp t4_break
t4:
    out pins, 4         ; pins := pins_t4
t4_loop:
    jmp y--, t4_loop     [loop_delay+1]
t4_break:
    jmp !osre new_command
.wrap